plugins {
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'maven'
    id 'signing'
}

repositories {
    mavenCentral()
}

group = "io.github.scottg489"
archivesBaseName = "regex-directed-graph-line-parser"
version = "0.3.6"

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.4'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'junit:junit:4.12'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://central.sonatype.com/api/v1/publisher") {
                authentication(userName: System.getenv("OSSRH_USERNAME"), password: System.getenv("OSSRH_PASSWORD"))
            }

            snapshotRepository(url: "https://central.sonatype.com/repository/maven-snapshots/") {
                authentication(userName: System.getenv("OSSRH_USERNAME"), password: System.getenv("OSSRH_PASSWORD"))
            }

            pom.project {
                name 'regex-directed-graph-line-parser'
                packaging 'jar'
                description 'A library for parsing lines in an ordered defined by a matching regular expression.'
                url 'https://github.com/ScottG489/regex-directed-graph-line-parser'

                scm {
                    connection 'git@github.com:ScottG489/regex-directed-graph-line-parser.git'
                    developerConnection 'git@github.com:ScottG489/regex-directed-graph-line-parser.git'
                    url 'https://github.com/ScottG489/regex-directed-graph-line-parser'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'scottg489'
                        name 'Scott Giminiani'
                        email 'scottg489@gmail.com'
                    }
                }
            }
        }
    }
}
